
Функция ДанныеHDVINNConCustCountTender()

	//Заполнить Признак гос. учереждения в карточках неактивных контрагентов по данным HWC
	ТаблицаДанныхHDW = Новый ТаблицаЗначений;
	ТаблицаДанныхHDW.Колонки.Добавить("INNCust", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(16)));			
	ТаблицаДанныхHDW.Колонки.Добавить("CountTender", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15)));			

	ИмяВременнойТаблицы = "HDV_reg_T" + Формат(НомерСеансаИнформационнойБазы(), "ЧГ=");
	
	мADOСоединение = _HDW.LancetHDWConnect();
	
	Попытка
		
		ТекстЗапроса =
		"IF OBJECT_ID('tempdb..##HDV_reg_T') IS NOT NULL
		|	DROP TABLE ##HDV_reg_T;";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "HDV_reg_T", ИмяВременнойТаблицы);
		РегистрацияИзмененийСделанныхВОбъектах.ВыполнитьПакетЗапроса(мADOСоединение, ТекстЗапроса);
		
		ТекстЗапроса =
		"CREATE TABLE ##HDV_reg_T
		|(
		|	INNKA [varchar](14) NOT NULL,
		|	CONSTRAINT [PK_HDV_reg_T] PRIMARY KEY CLUSTERED
		|	(
		|		INNKA ASC
		|	)
		|);";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "HDV_reg_T", ИмяВременнойТаблицы);
		РегистрацияИзмененийСделанныхВОбъектах.ВыполнитьПакетЗапроса(мADOСоединение, ТекстЗапроса);
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Контрагенты.ИНН КАК ИНН
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	НЕ Контрагенты._Активен
		|	И Контрагенты.ИНН <> """"
		|	И НЕ Контрагенты._ПризнакГосУчреждения
		|
		|СГРУППИРОВАТЬ ПО
		|	Контрагенты.ИНН";
		
		ТекстЗапроса =
		"INSERT INTO ##HDV_reg_T (INNKA)
		|	values ";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "HDV_reg_T", ИмяВременнойТаблицы);
		
		Выборка = Запрос.Выполнить().Выбрать();
		МассивИНН = Новый Массив;
		Сч = 0;
		Пока Выборка.Следующий() Цикл
			
			МассивИНН.Добавить(СокрЛП(Выборка.ИНН));
			Сч = Сч + 1;
			
			Если Сч > 250 Тогда
				ТекстЗапросаДобавить = ТекстЗапроса + "('" + СтрСоединить(МассивИНН, "'),('") + "')";
				ВыполнитьПакетЗапроса(мADOСоединение, ТекстЗапросаДобавить);
				МассивИНН = Новый Массив;
				Сч = 0;
			КонецЕсли;
			
		КонецЦикла;

		Если МассивИНН.Количество() > 0 Тогда
			ТекстЗапросаДобавить = ТекстЗапроса + "('" + СтрСоединить(МассивИНН, "'),('") + "')";
			ВыполнитьПакетЗапроса(мADOСоединение, ТекстЗапросаДобавить);
			МассивИНН = Новый Массив;
		КонецЕсли;
		
		ТекстЗапроса =
		"SELECT
		|	a.INNConCust as INNCust,
		|	count(distinct(tender_id)) as CountTender
		|FROM dbo.export_lancetAll as a
		|	INNER JOIN [dbo].[Contracts] as cont
		|	ON cont.[ContractIdNx] = a.[ContractIdNx]
		|	INNER JOIN ##HDV_reg_T as reg_T
		|	ON reg_T.INNKA = cont.InnConCust
		|WHERE
		|	a.FormT_name = 'открытый аукцион в электронной форме'
		|GROUP BY
		|	a.INNConCust";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "HDV_reg_T", ИмяВременнойТаблицы);
		
		Запись = Новый COMОбъект("ADODB.RecordSet");
		Запись.CursorLocation = 3;
		Запись.LockType = 2;
		
		Запись.Open(ТекстЗапроса, мADOСоединение); 	
		Если Запись.RecordCount > 0 Тогда
			Запись.MoveFirst();
		КонецЕсли;
		
		Пока Не Запись.EOF Цикл
			НоваяСтрока = ТаблицаДанныхHDW.Добавить();
			Для i = 0 по Запись.Fields.Count-1 Цикл
				НоваяСтрока[Запись.Fields(i).Name] = Запись.Fields(i).Value;		
			КонецЦикла;	
			Запись.MoveNext();
		КонецЦикла;
		
		Запись.Close();
		
		ТекстЗапроса =
		"IF OBJECT_ID('tempdb..##HDV_reg_T') IS NOT NULL
		|	DROP TABLE ##HDV_reg_T;";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "HDV_reg_T", ИмяВременнойТаблицы);
		РегистрацияИзмененийСделанныхВОбъектах.ВыполнитьПакетЗапроса(мADOСоединение, ТекстЗапроса);
		
		РегистрацияИзмененийСделанныхВОбъектах.ADOClose(мADOСоединение);
		
	Исключение
		
		ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		
		Попытка
			ТекстЗапроса =
			"IF OBJECT_ID('tempdb..##HDV_reg_T') IS NOT NULL
			|	DROP TABLE ##HDV_reg_T;";
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "HDV_reg_T", ИмяВременнойТаблицы);
			РегистрацияИзмененийСделанныхВОбъектах.ВыполнитьПакетЗапроса(мADOСоединение, ТекстЗапроса);
		Исключение
		КонецПопытки;
		
		РегистрацияИзмененийСделанныхВОбъектах.ADOClose(мADOСоединение);
		
		ВызватьИсключение (ТекстОшибки);
		
	КонецПопытки;
	
	Возврат ТаблицаДанныхHDW;
	
КонецФункции

